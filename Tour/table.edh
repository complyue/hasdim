
class MyTable { extends Table
  method __init__( name as this.name, ***_ ) pass
  method __repr__(** kwargs ) {
    '# ' ++ this.name ++ ' #\n'
    ++ super.__repr__(** kwargs )
  }
  method __show__(** kwargs ) {
    '# ' ++ this.name ++ ' #\n'
    ++ super.__show__(** kwargs )
  }
  method __desc__(** kwargs ) {
    '# ' ++ this.name ++ ' #\n'
    ++ super.__desc__(** kwargs )
  }
}

# %%
tbl = MyTable( name= 'My Demo Table',
  capacity= 10, columns= (
    rowidx= arange( 10 ),
    rownum= '#' + arange( 1..10, dtype= box ),
    sq= arange( 10, dtype= f8 ) **2,
    rowdesc= () =< ( # this round bracket
      # intends for its content indented by Edh formatter
      for i from 0:10 do 'Square of ' ++ i ++ ' is ' ++ i**2
    ),
  ), row'count= 8,
)

# %%
tbl @| ( columnWidth=( 6, rowdesc=20, ) ) @| show


# %%
tbl[ 2 ]

# %%
tbl[ 2:7:2 ] | show

# %%
tbl.sq > 5 && tbl.sq < 20 | where | show

# %%
tbl[ tbl.sq > 5 && tbl.sq < 20 ] | show

# %%
tbl[ where $ tbl.sq > 5 && tbl.sq < 20 ] | show

# %%
tbl.rowdesc|show

# %%
mark( tbl, 7 )
tbl|show

# %%
tbl.sq|desc

# %%
tbl.data1 = 3 ** arange( 20, dtype=f4 )
tbl.data1 | show

# %%
tbl.data2 = float64
tbl.data2[] = 55
tbl.data2 | show

# %%
tbl|show

# %%
tbl.columns

# %%
tbl[ 2:5 ] = 77
tbl|show

# %%
tbl[ 2:5 ] =( sq=33 )
tbl|show

# %%
tbl[ 2:5 ] =( rownum= '#' + arange( 99:96, dtype=box ) )
tbl|show
